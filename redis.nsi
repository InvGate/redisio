; Script generated by the HM NIS Edit Script Wizard.

!include "x64.nsh"

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "RedisIO"
!define PRODUCT_VERSION "2.6.12"
!define PRODUCT_PUBLISHER "InvGate S.R.L."
!define PRODUCT_WEB_SITE "http://www.invgate.com"
!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths\redis-server.exe"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"

Name "${PRODUCT_NAME}" ; ${PRODUCT_VERSION}"
OutFile "build/redisio.exe"
LoadLanguageFile "${NSISDIR}\Contrib\Language files\English.nlf"
LoadLanguageFile "${NSISDIR}\Contrib\Language files\Spanish.nlf"
InstallDir "$PROGRAMFILES\RedisIO"

Function .onInit
  ${If} ${RunningX64}
    SetRegView 64
    strcpy $INSTDIR "$PROGRAMFILES64\RedisIO"
  ${Else}

  ${EndIf}
FunctionEnd

Icon "redis.ico"
UninstallIcon "${NSISDIR}\Contrib\Graphics\Icons\nsis1-uninstall.ico"
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" ""
DirText "Se va a instalar $(^Name) en el siguiente directorio.$\r$\n$\r$\nPara instalar en un directorio distinto, presione Examinar y seleccione otro directorio"
ShowInstDetails show
ShowUnInstDetails show

Section "RedisIO" SEC01
  SetOutPath "$INSTDIR"
  SetOverwrite try
  File "nssm.exe"
  SetOutPath "$INSTDIR"
  ${If} ${RunningX64}
    File "redis-server-64.exe"
  ${Else}
    File "redis-server-32.exe"
  ${EndIf}
SectionEnd

Section -Post
  ${If} ${RunningX64}
    ExecWait '$INSTDIR\nssm.exe install RedisIO "$INSTDIR\redis-server-64.exe"'
  ${Else}
    ExecWait '$INSTDIR\nssm.exe install RedisIO "$INSTDIR\redis-server-32.exe"'
  ${EndIf}
  ExecWait '"sc.exe" config RedisIO DisplayName= "RedisIO Server"'
  ExecWait '"sc.exe" start RedisIO'
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "" "$INSTDIR\nssm.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayIcon" "$INSTDIR\redis-server.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
SectionEnd


Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "La desinstalación de $(^Name) finalizó satisfactoriamente."
FunctionEnd

Function un.onInit
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "¿Desea desinstalar $(^Name) junto con todos sus componentes?" IDYES +2
  Abort
FunctionEnd

Section Uninstall
  ExecWait '"sc.exe" stop RedisIO'
  ExecWait "$INSTDIR\nssm.exe remove RedisIO confirm"
  Delete "$INSTDIR\uninst.exe"
  ${If} ${RunningX64}
    Delete "$INSTDIR\redis-server-64.exe"
  ${Else}
    Delete "$INSTDIR\redis-server-32.exe"
  ${EndIf}
  Delete "$INSTDIR\nssm.exe"

  RMDir "$INSTDIR"

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  DeleteRegKey HKLM "${PRODUCT_DIR_REGKEY}"
  SetAutoClose true
SectionEnd